buildscript {
    ext {
        springBootVersion = "1.2.7.RELEASE"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

group "com.daggerok"
version "1.0"

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "spring-boot"

jar {
    baseName = "blog"
    version = "1.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-hateoas",
            "org.springframework.boot:spring-boot-starter-data-rest",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            // spring boot metrics and maintain
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-remote-shell",
            // code gen
            "org.projectlombok:lombok:1.16.6",
            // guava cache
            "org.springframework:spring-context-support:4.2.2.RELEASE",
            "com.google.guava:guava:18.0",
            // java 8 jsr310 postDate api
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.3"

    runtime "com.h2database:h2"

    testCompile "org.springframework.boot:spring-boot-starter-test",
                "org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RC1"
}

import org.apache.tools.ant.taskdefs.condition.Os

task www(type: Exec) {
    def scriptName = "build"

    println "starting npm install and www build..."

    workingDir "${project.projectDir}/www"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "cmd", "/c", "${scriptName}.bat" // test.me
    } else {
        commandLine "./${scriptName}.sh"
    }
    // print to console
    standardOutput = new ByteArrayOutputStream()
    // use npm.output() to get output
    ext.output = { return standardOutput.toString() }

    ignoreExitValue = false
}

build.dependsOn www
build.shouldRunAfter www
www.shouldRunAfter clean
